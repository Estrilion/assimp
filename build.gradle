import static org.gradle.internal.os.OperatingSystem.*

plugins {
    id 'java'
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id "com.github.johnrengelman.shadow" version '5.2.0'
}

// jitpack
group = 'com.github.kotlin_graphics'

ext {
    moduleName = "${group}.assimp"
    kot = 'org.jetbrains.kotlin:kotlin'
    kx = "com.github.kotlin-graphics"
    kotlin_version = '1.3.70'
    kotlintest_version = '3.4.2'
    uno_version = '0cf66d34a0e2c94e1184528c2fa1bf23c15ff936'
    vkk_version = '326f36ec13234cc93056980f4fc59ab3b7c4574f'
    gln_version = '40443438c4a1d3018f96c80dba4488d47ea2c777'
    gli_version = 'aadbe89bf42f801c2ea55c5a38a2e748f7acdf14'
    glm_version = '1b4ac18dd1a3c23440d3f33596688aac60bc0141'
    unsigned_version = '18131d0fe0b7465a145a4502d31452c5ae0e59a1'
    kool_version = 'fcf04b2c03b8949d9d9a8b0a580082e927903510'
    lwjgl_version = "3.2.3"
    lwjgl_natives = current() == WINDOWS ? "windows" : current() == LINUX ? "linux" : "macos"
}

dependencies {

    implementation "$kot-stdlib-jdk8"
    implementation "$kot-reflect"

    implementation "$kx:uno-sdk:$uno_version"
    implementation "$kx:gli:$gli_version"
    implementation "${kx}.glm:glm:$glm_version"
    testImplementation "${kx}.glm:glm-test:$glm_version"
    implementation "$kx:kool:$kool_version"
    implementation "$kx:kotlin-unsigned:$unsigned_version"

    testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintest_version".toString()

    implementation 'io.github.microutils:kotlin-logging:1.7.8'

//    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
//    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'

    //implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
	implementation 'org.slf4j:slf4j-api:1.7.29'
    testImplementation 'org.slf4j:slf4j-simple:1.7.29'

    ["", "-glfw", "-jemalloc", "-openal", "-opengl", "-stb"].each {
        implementation "org.lwjgl:lwjgl$it:$lwjgl_version"
        runtimeOnly "org.lwjgl:lwjgl$it:$lwjgl_version:natives-$lwjgl_natives"
    }
}

repositories {
    mavenCentral()
    maven { setUrl("https://dl.bintray.com/kotlin/kotlin-dev") }
    maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    maven { setUrl('https://jitpack.io') }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

test {
    useJUnitPlatform()

    maxHeapSize = "1G"
//    systemProperties['junit.jupiter.execution.parallel.enabled'] = true
//    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
//    forkEvery = 1
}

artifacts {
    archives sourcesJar
//    archives javadocJar
}

shadowJar.archiveClassifier = 'all'

compileKotlin {
    kotlinOptions.jvmTarget = "11"
    destinationDir = compileJava.destinationDir
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
    destinationDir = compileTestJava.destinationDir
}

compileJava {
    dependsOn(':compileKotlin')
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--patch-module', "$moduleName=${sourceSets["main"].output.asPath}"]
        classpath = files()
    }
}

jar { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }